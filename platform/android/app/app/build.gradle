plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def ndkDir = properties.getProperty('ndk.dir')

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "de.k0bin.sourcerenderer"
        minSdkVersion 24
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    ndkVersion '22.1.7171670'
}

task buildRust(type: Exec) {
    def commands = [ "cargo", "ndk", "-t", "arm64-v8a", "-t", "x86_64", "-o", "../app/app/src/main/jniLibs", "build" ]
    if (!gradle.startParameter.taskNames.any { it.contains("assembleDebug") }) {
        commands.add("--release")
    }

    environment "ANDROID_NDK_HOME", ndkDir
    workingDir "../../lib"
    commandLine commands
}

tasks.whenTaskAdded { task ->
    if (task.name == 'javaPreCompileDebug' || task.name == 'javaPreCompileRelease' || task.name == "compileDebugSources" || task.name == "compileReleaseSources") {
        task.dependsOn 'buildRust'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.activity:activity-ktx:1.4.0'
    implementation 'androidx.fragment:fragment-ktx:1.3.6'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
